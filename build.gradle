plugins {
    id "org.springframework.boot" version "2.1.9.RELEASE" apply false
    id "org.flywaydb.flyway" version "6.1.1" apply false
}

subprojects { subProj ->
    ext {
        timesheetsDevDb = "jdbc:mysql://localhost:3306/timesheets_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
        timesheetsTestDb = "jdbc:mysql://localhost:3306/timesheets_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
        registrationDevDb = "jdbc:mysql://localhost:3306/registration_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
        registrationTestDb = "jdbc:mysql://localhost:3306/registration_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
    }

    if (isNotAJavaProject(subProj)) return

    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:Greenwich.SR3")
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.9.RELEASE")
        }
    }

    dependencies {
        testCompile("org.mockito:mockito-core")
        testCompile("junit:junit")
        testCompile("org.assertj:assertj-core")
    }
}

static boolean isNotAJavaProject(Project project) {
    ["applications", "components", "platform-services"].contains(project.name) || project.path.startsWith(":databases")
}
